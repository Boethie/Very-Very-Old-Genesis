buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

file("build.properties").withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

// Snapshot versioning - Format: <Year>w<Week of Year>u<Day of Week>, Example: 14w23u5
def date = new Date()
version = date.format("yy") + "w" + date.format("w") + "u" + date.format("u")
group = "genesis"

minecraft {
    version = "${config.forge_version}"
    runDir = "eclipse"

    replaceIn "Genesis.java"
    replace "@VERSION@", project.version
}

// set archive base name here, otherwise minecraft version will be null
archivesBaseName = "genesis-" + project.minecraft.version

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
